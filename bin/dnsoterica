#!perl
# ABSTRACT: run dns-o!

# This goofy linebreak prevents PAUSE from indexing this package.  The
        # package statement is here to help Pod::Weaver. -- rjbs, 2017-08-02
package
  dnsoterica;

use strict;
use warnings;

use DNS::Oterica::App;
use File::Find::Rule;
use Getopt::Long::Descriptive;
use YAML::XS ();

my ($opts, $usage) = describe_options(
  "usage: %c %o <root>",
  [ 'ns-family=s', 'DNS::Oterica node family for NS records',
    { required => 1, } ],
  [ 'hostmaster=s', 'hostmaster email',
    { required => 1, } ],
);

my $root = shift @ARGV;
$usage->die if @ARGV;

my $app = DNS::Oterica::App->new(
  root          => $root,
  hub_args      => $opts,
);

$app->populate_networks;
$app->populate_domains;
$app->populate_hosts;

my @nodes = sort { $a->fqdn cmp $b->fqdn } $app->hub->nodes;

print sort map {; $_->as_data_lines } $app->hub->networks;
print "\n";
print $_->as_data_lines for @nodes;
print "\n";
print $_->as_data_lines, "\n" for $app->hub->node_families;
