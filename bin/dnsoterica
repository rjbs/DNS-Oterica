#!/icg/bin/perl
# ABSTRACT: run dns-o!
use strict;
use warnings;
use lib 'lib'; # XXX: temporary for now -- rjbs, 2009-01-30

use DNS::Oterica::App;
use File::Find::Rule;
use Getopt::Long::Descriptive;
use YAML::XS ();

my ($opts, $usage) = describe_options(
  "usage: %c %o <root>",
  [ 'ns-family=s', 'DNS::Oterica node family for NS records',
    { required => 1, } ],
  [ 'hostmaster=s', 'hostmaster email',
    { required => 1, } ],
);

my $root = shift @ARGV;
$usage->die if @ARGV;

my $app = DNS::Oterica::App->new(
  root          => $root,
  hub_args      => $opts,
);

$app->populate_locations;
$app->populate_domains;
$app->populate_hosts;

my @nodes = sort { $a->fqdn cmp $b->fqdn } $app->hub->nodes;

print $_->as_data_lines for @nodes;
print "\n";
print $_->as_data_lines, "\n" for $app->hub->node_families;
